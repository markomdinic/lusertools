#!/usr/bin/perl
#
# lusertools (Tools for managing user accounts and groups stored in LDAP)
#
# Copyright (c) 2019 Marko Dinic. All rights reserved.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
###############################################################################

our $VERSION = "2.1.0";

###############################################################################

our ($ABSPATH, $PREFIX, $COMMAND, $LIBDIR, $SYSCONFDIR, $LOCALSTATEDIR);

BEGIN {

    use Cwd qw(abs_path);
    $ABSPATH = abs_path($0);
    ($PREFIX, $COMMAND) = ($ABSPATH =~ /^(?:(.*)\/)?([^\/]+)$/);

###############################################################################
# DEFAULT PATHS BEGIN (change if neccessary)
###############################################################################


    $LIBDIR = $PREFIX.'/lib';
    $SYSCONFDIR = $PREFIX.'/etc';
    $LOCALSTATEDIR = '/var';


###############################################################################
# DEFAULT PATHS END (don't change anything below this line)
###############################################################################

}

########################################################################################

use strict;
use warnings;

########################################################################################

use Getopt::Long qw(:config no_ignore_case);

use Net::LDAP;
use Net::LDAPS;
use Net::LDAP::Control::Sort;
use Net::LDAP::Constant qw(LDAP_SIZELIMIT_EXCEEDED);

########################################################################################
# DEFAULTS
########################################################################################

our $config_file = "/etc/lusertools/ldap.conf";
our $secret_file = "/etc/lusertools/ldap.secret";

########################################################################################

our %config = (
    'group_objectclasses'	=> [ 
				    'top',
				    'posixGroup',
				    'sambaDomain'
				   ],

    'user_naming_attr'		=> 'uid',
    'group_naming_attr'		=> 'cn',
    'uid_attr'			=> 'uidNumber',
    'gid_attr'			=> 'gidNumber',
    'samba_sid_attr'		=> 'sambaSID',
    'description_attr'		=> 'description',
    'group_member_attr'		=> 'memberUid'
);

########################################################################################

sub format_table($$$)
{
    my ($columns, $spacing, $offset) = @_;
    my @table = ();

    while(1) {
	my @row = ();
	my $i = 0;
	foreach my $col (@{$columns}) {
	    my $line = '';
	    my $data = $col->{'data'};
	    my $func = $col->{'func'};
	    while(defined(my $chunk = shift(@{$data}))) {
		if(defined($func) && ref($func) eq 'CODE') {
		    $chunk = $func->($chunk);
		}
		$line .= $chunk.(scalar(@{$data}) ? $col->{'delim'}:'');
		last if($col->{'width'} - length($line) < 5);
	    }
	    push @row, $line;
	    $i++ if $line ne '';
	}
	last unless $i;
	push @table, \@row;
    }
    
    my $formatted = '';
    
    while(defined(my $row = shift(@table))) {
	while(defined(my $col = shift @{$row})) {
	    my $padding = $spacing - length($col);
	    $padding = 1 if($padding < 1);
	    $formatted .= $col.(scalar(@{$row}) ? (' 'x$padding):'');
	}
	$formatted .= scalar(@table) ? "\n".(' 'x$offset):'';
    }

    return $formatted;
}

########################################################################################

sub ldap_bind()
{
    my $binddn = $config{'bind_dn'};
    my $bindpw = $config{'bind_pw'};

    my $port = (defined($config{'ldap_port'}) && $config{'ldap_port'}=~ /^\d+$/ &&
		$config{'ldap_port'} > 0 && $config{'ldap_port'} < 65536) ? $config{'ldap_port'}:undef,

    my $ldap;

    # If SSL is requested ...
    if($config{'use_ssl'}) {
	# ... connect to LDAP master over SSL
	$ldap = Net::LDAPS->new($config{'ldap_server'},
				'ldap_port' => defined($port) ? $port:636,
				'verity' => 'none');
    # ... otherwise ...
    } else {
	# ... just connect to LDAP master
	$ldap = Net::LDAP->new($config{'ldap_server'},
			       'ldap_port' => defined($port) ? $port:389);
    }

    return undef unless defined($ldap);

    # Login to LDAP master
    my $res = $ldap->bind($binddn, 'password' => $bindpw);
    return undef unless defined($res);
    if($res->code > 0) {
	print STDERR $res->error."\n";
	$ldap->unbind;
	return undef;
    }

    return $ldap;
}

sub ldap_unbind($)
{
    $_[0]->unbind;
}

########################################################################################

sub get_uid($$)
{
    my ($ldap, $username) = @_;

    # Search for specific user by username
    my $res = $ldap->search('base'      => $config{'users_dn'},
			    'scope'     => 'one',
			    'filter'    => sprintf('(%s=%s)', $config{'user_naming_attr'}, $username),
			    'attrs'     => [ $config{'uid_attr'} ],
			    'sizelimit' => 1);

    return undef unless defined($res);

    if($res->code > 0) {
	# We can ignore 'size limit exceeded' error, but not others
	if($res->code != LDAP_SIZELIMIT_EXCEEDED) {
	    print STDERR $res->error."\n";
	    return undef;
	}
    }

    # Get user's LDAP object
    my ($user) = $res->entries;
    # Get UID
    return $user->get_value($config{'uid_attr'});
}

sub list_groups($)
{
    my $ldap = shift;

    # This is a Server Side Sorting (SSS) control.
    # We want to do a search with results sorted by
    # GIDs in ascending order
    my $sort = Net::LDAP::Control::Sort->new('order' => $config{'gid_attr'});

    # Get the list of groups
    my $res = $ldap->search('base'    => $config{'groups_dn'},
			    'scope'   => 'one',
			    'filter'  => $config{'group_objectclass_filter'},
			    'control' => [ $sort ]);

    return undef unless defined($res);

    if($res->code > 0) {
	# We can ignore 'size limit exceeded' error, but not others
	if($res->code != LDAP_SIZELIMIT_EXCEEDED) {
	    print STDERR $res->error."\n";
	    return undef;
	}
    }

    # Print header
    printf("%-16s %-8s %-30s %s\n%s\n",
	   'Group Name',
	   'GID',
	   'Description',
	   'Additional Members',
	   '-'x100);

    # Print groups' metadata
    foreach my $group ($res->entries) {

	my $groupname = $group->get_value($config{'group_naming_attr'});
	my $description = $group->get_value($config{'description_attr'});
	my $gid = $group->get_value($config{'gid_attr'});
	my @members = $group->get_value($config{'group_member_attr'});

	# Put description and group members into columns
	# of certain width, which will cause text to be 
	# broken into multiple lines, if it's length
	# exceeds column width
	my $cols = [
	    # Description
	    {
		'data' => [ split(/\s+/, defined($description) ? $description:'') ],
		'width' => 30,
		'delim' => ' '
	    },
	    # Group members
	    {
		'data' => \@members,
		'width' => 30,
		'delim' => ', ',
		'func' => sub {
		    my $username = shift;

		    my $uid = get_uid($ldap, $username);
		    if(defined($uid) && $uid ne '') {
			$username .= ' ('.$uid.')';
		    }

		    return $username;
		}
	    }
	];

	# Put columns into a table with an offset
	# of 26 characters from the beginning of
	# each line and a max of 31 characters of
	# spacing between columns
	my $list = format_table($cols, 31 , 26); 

	printf("%-16s %-8d %-24s\n",
		defined($groupname) ? $groupname:'',
		defined($gid) ? $gid:'',
		$list);

    }

}

########################################################################################

sub get_config($$)
{
    my ($configfile, $secretfile) = @_;
    local *CONF;

    # Make sure configuration file exists
    unless(-f $configfile) {
	print STDERR "Configuration file is missing.\n";
	return 0;
    }

    # Open config file
    unless(open(CONF, $configfile)) {
	print STDERR "Couldn't open configuration file ".$configfile."\n";
	return 0;
    }

    # Load configuration
    while(<CONF>) {
	chop;
	$_ =~ s/(#.+)$//g;				 # Strip comments
	$_ =~ s/^\s+//g;				 # Strip leading spaces
	$_ =~ s/\s+$//g;				 # Strip trailing spaces
	my ($opt, $arg) = ($_ =~ /^(\S+)(?:\s+(.*))?$/); # Split config line string

	next unless(defined($opt) && $opt ne '');

	# If configuration option's argument is missing,
	# that option will be completely ignored, even
	# if it has a default setting.
	unless(defined($arg) && $arg ne '') {
	    delete $config{$opt} if exists $config{$opt};
	# If configuration option is prefixed with '!',
	# that option will be completely ignored, even
	# if it has a default setting.
	} elsif($opt =~ /^\!(.*)$/) {
	    delete $config{$1} if($1 ne '' && exists $config{$1});
	} elsif($opt eq 'use_ssl') {
	    $config{$opt} = ($arg =~ /^on|yes|true|1$/i) ? 1:0;
	} elsif($opt =~ /^(?:user|group|role)\_objectclasses$/) {
	    $config{$opt} = [ (split(/,/, $arg)) ];
	} elsif($opt eq 'minimum_gid') {
	    $config{$opt} = ($arg =~ /^\d+$/ && $arg > 0) ? $arg:1000;
	} else {
	    $config{$opt} = $arg;
	}
    }

    # Close config file
    close(CONF);

    # Make sure loaded configuration didn't mess up object classes
    unless(defined($config{'group_objectclasses'}) && scalar(@{$config{'group_objectclasses'}}) > 0) {
	print STDERR "At least one group object class must be defined.\n";
	return 0;
    }

    # If we were invoked by root ...
    if($> == 0 && $< == 0) {
	# ... make sure secret file exists
	unless(-f $secretfile) {
	    print STDERR "Secret file is missing.\n";
	    return 0;
	}
	# ... open LDAP bind secret file
	unless(open(CONF, $secretfile)) {
	    print STDERR "Couldn't open secret file ".$secretfile."\n";
	    return 0;
	}
	# ... get LDAP bind secret
	my $secret = <CONF>;
	chomp $secret;
	# ...  close LDAP bind secret file
	close(CONF);
	# ... use root DN as bind DN
	$config{'bind_dn'} = $config{'root_dn'};
	# ... add LDAP secret to the configuration
	$config{'bind_pw'} = $secret;
    }

    unless(defined($config{'ldap_server'}) && $config{'ldap_server'} ne '') {
	print STDERR "LDAP server not configured properly.\n";
	return 0;
    }

    unless(defined($config{'bind_dn'}) && $config{'bind_dn'} ne '' &&
	   defined($config{'bind_pw'}) && $config{'bind_pw'} ne '') {
	print STDERR "Invalid LDAP credentials.\n";
	return 0;
    }

    unless(defined($config{'groups_dn'}) && $config{'groups_dn'} ne '') {
	print STDERR "Groups base DN not configured properly.\n";
	return 0;
    }

    # Generate object class filter for search operations
    $config{'group_objectclass_filter'} = '(&(objectClass='.join(')(objectClass=', @{$config{'group_objectclasses'}}).'))';

    return 1;
}

########################################################################################

sub usage()
{
print <<EOF 
Usage: $COMMAND [OPTIONS]

OPTIONS:

 -C, --config <config file>
 -S, --secret <secret file>

 -h, --help
 -v, --version

EOF
;

    exit(1);
}

sub version()
{
    print "lusertools (version ".$VERSION.")\n\n";
    usage();
}

########################################################################################
#                                       M A I N
########################################################################################

my ($res, $help, $ver);

# Get command line arguments, if any
$res = GetOptions('help|h'		=> \$help,
		  'version|v'		=> \$ver,
		  'config|C=s'		=> \$config_file,
		  'secret|S=s'		=> \$secret_file);

# Show help if requested
usage() if(!$res || $help);
# Show version, if requested
version() if $ver;

# If command line arguments are wrong, show help
usage() if(scalar(@ARGV) > 0);

# Load config file
get_config($config_file, $secret_file)
  or exit(4);

$res = 0;

# Connect and bind to LDAP server
my $ldap = ldap_bind();
unless(defined($ldap)) {
    print STDERR "Failed to connect to LDAP master.\n";
    exit(5);
}

# List group objects
list_groups($ldap);

# Unbind from server
ldap_unbind($ldap);

exit($res ? 0:255);
